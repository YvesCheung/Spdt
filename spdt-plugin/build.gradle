apply plugin: 'groovy'
apply plugin: 'maven'

sourceSets {
    main {
        java {
            include '**/*.groovy'
        }
    }
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    compileOnly gradleApi()
    compile localGroovy()
    compile "com.android.tools.build:gradle:${gradle_version}"
    compile 'com.squareup:kotlinpoet:1.4.4'

    compile project(":spdt-data")
}

compileJava {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

ext {
    libraryGroupId = 'com.github.YvesCheung.Spdt'
    libraryName = 'spdt-plugin'

    libraryDescription = 'Build variants with different source code & resource.'

    siteUrl = 'https://github.com/YvesCheung/Spdt'
    gitUrl = 'https://github.com/YvesCheung/Spdt.git'

    libraryVersion = version

    developerId = 'YvesCheung'
    developerName = 'YvesCheung'
    developerEmail = '975135274@qq.com'
}

apply from: '../gradle/publish.gradle'

task generateBuildConfig {
    doLast {
        String code = """
package com.unionyy.mobile.plugin

//This file is generated by build.gradle. Don't edit!!!
class BuildConfig {
    static final String version = "$version"
    static final String groupId = "$group"
}
"""
        File generateFile =
            file("./src/main/groovy/com/unionyy/mobile/plugin/BuildConfig.groovy")
        generateFile.parentFile.mkdirs()
        generateFile.write(code)
        println("generate file $generateFile")
    }
}
tasks.findByName("install").dependsOn(generateBuildConfig)

//./gradlew :spdt-plugin:clean :spdt-plugin:publishToMavenLocal

//插件修改后运行前需要重新发布: ./gradlew -p spdt-plugin clean build uploadArchives --info

